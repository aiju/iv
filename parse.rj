bool kwtab[string] = {
	"always": true, "and": true, "assign": true, 
	"automatic": true, "begin": true, "buf": true, "bufif0": true, 
	"bufif1": true, "case": true, "casex": true, "casez": true, 
	"cell": true, "cmos": true, "config": true, "deassign": true, 
	"default": true, "defparam": true, "design": true, "disable": true, 
	"edge": true, "else": true, "end": true, "endcase": true, 
	"endconfig": true, "endfunction": true, "endgenerate": true, "endmodule": true, 
	"endprimitive": true, "endspecify": true, "endtable": true, "endtask": true, 
	"event": true, "for": true, "force": true, "forever": true, 
	"fork": true, "function": true, "generate": true, "genvar": true, 
	"highz0": true, "highz1": true, "if": true, "ifnone": true, 
	"incdir": true, "include": true, "initial": true, "inout": true, 
	"input": true, "instance": true, "integer": true, "join": true, 
	"large": true, "liblist": true, "library": true, "localparam": true, 
	"macromodule": true, "medium": true, "module": true, "nand": true, 
	"negedge": true, "nmos": true, "nor": true, "noshowcancelled": true, 
	"not": true, "notif0": true, "notif1": true, "or": true, 
	"output": true, "parameter": true, "pmos": true, "posedge": true, 
	"primitive": true, "pull0": true, "pull1": true, "pulldown": true, 
	"pullup": true, "pulsestyle_ondetect": true, "pulsestyle_onevent": true, "rcmos": true, 
	"real": true, "realtime": true, "reg": true, "release": true, 
	"repeat": true, "rnmos": true, "rpmos": true, "rtran": true, 
	"rtranif0": true, "rtranif1": true, "scalared": true, "showcancelled": true, 
	"signed": true, "small": true, "specify": true, "specparam": true, 
	"strong0": true, "strong1": true, "supply0": true, "supply1": true, 
	"table": true, "task": true, "time": true, "tran": true, 
	"tranif0": true, "tranif1": true, "tri": true, "tri0": true, 
	"tri1": true, "triand": true, "trior": true, "trireg": true, 
	"unsigned1": true, "use": true, "uwire": true, "vectored": true, 
	"wait": true, "wand": true, "weak0": true, "weak1": true, 
	"while": true, "wire": true, "wor": true, "xnor": true, 
	"xor": true
};

struct Symbol;
struct Type;
struct Const;

struct Node {
	enum NodeType {
		ASTMODULE,
		ASTASSIGN,
		ASTSYM,
		ASTBIN,
		ASTCINT,
		ASTCONCAT,
		ASTNUM,
		ASTUN,
		ASTIDX,
		ASTREPL,
		ASTTERN,
	} t;
	Type type;
	bool isconst;
	Node stat[];
	string op;
	Node n1, n2, n3;
	Symbol sym;
	Node l[];
	Const num;
	int numi;
	int lineno;
};

struct Type {
	enum TypeType {
		TYPBIT,
		TYPBITS,
		TYPBITV,
		TYPUNSZ,
		TYPEVENT,
		TYPREAL,
		TYPMEM
	} t;
	bool sign;
	Node lo;
	Node hi;
	Node sz;
	Type elem;
};

struct Token {
	string t;
	string str;
	Symbol sym;
	int base;
	bool sign;
};

void
Type(TypeType t, ... bool sign, Node lo, Node hi, Type elem)
{
	this.t = t;
	this.sign = sign;
	if(t == TYPBITV){
		this.lo = lo;
		this.hi = hi;
	}else{
		this.lo = null;
		this.hi = null;
	}
	if(t == TYPBITV)
		this.sz = new Node(Node{t: ASTBIN, op: "+", n1: new Node(Node{t: ASTBIN, op: "-", n1: this.hi, n2: this.lo}), n2: new Node(Node{t: ASTCINT, numi: 1})});
	else if(t == TYPBITS)
		this.sz = lo;
	else if(t == TYPBIT)
		this.sz = new Node(Node{t: ASTCINT, numi: 1});
	else
		this.sz = null;
	this.elem = elem;
}

Type bittype = new Type(TYPBIT, false);
Type sbittype = new Type(TYPBIT, true);
Type eventtype = new Type(TYPEVENT, false);
Type unsztype = new Type(TYPUNSZ, false);
Type sunsztype = new Type(TYPUNSZ, true);
Type realtype = new Type(TYPREAL);

string
Type.toDebug()
{
	switch(this.t){
	case TYPBIT:
	case TYPUNSZ:
		return (string)this.t + (this.sign ? " signed" : "");
	case TYPBITV:
		return (string)this.t + (this.sign ? " signed [" : " [") + this.hi.toString() + ":" + this.lo.toString() + "]";
	case TYPBITS:
		return (string)this.t + (this.sign ? " signed [" : " [") + this.sz.toString() + "]";
	}
}

struct Const {
	string v;
	int sz;
	bool sign;
};

void
Const(string v, int sz, bool sign)
{
	this.v = v;
	this.sz = sz;
	this.sign = sign;
}

string
Const.toString()
{
	string s;
	int i;

	s = "";
	if(this.sz !== null)
		s += this.sz.toString();
	if(this.sign)
		s += "'sb";
	else
		s += "'b";
	for(i = 0; i < this.v.length; i++)
		s += this.v.charAt(this.v.length - 1 - i);
	return s;
}

function
Node(Node attr)
{
	string i;

	for(i in attr)
		this[i] = attr[i];
	this.isconst = false;
}

string
Node.toString(...string ind, int lvl)
{
	string s;
	var t;
	int i;

	if(ind === undefined) ind = "";
	if(lvl === undefined) lvl = 0;
	switch(this.t){
	case ASTMODULE:
		s = ind + "module;\n";
		for(i = 0; i < this.stat.length; i++)
			s += this.stat[i].toString(ind + "\t") + "\n";
		s += ind + "endmodule";
		return s;
	case ASTSYM:
		return this.sym.toString();
	case ASTBIN:
		t = optab[this.op];
		s = this.n1.toString(ind, t.prec) + " " + this.op + " " + this.n2.toString(ind, t.prec + 1);
		if(optab[this.op].prec < lvl)
			s = "(" + s + ")";
		return s;
	case ASTCONCAT:
		s = "{";
		for(i = 0; i < this.l.length; i++){
			if(i > 0) s += ", ";
			s += this.l[i].toString(ind, 0);
		}
		s += "}";
		return s;
	case ASTASSIGN:
		return this.n1.toString() + " = " + this.n2.toString() + ";";
	case ASTNUM:
		return this.num.toString();
	case ASTCINT:
		return this.numi.toString();
	case ASTUN:
		return this.op + this.n1.toString();
	default:
		return (string)this.t + "(...)";
	}
}

string
Node.toDebug(... string ind)
{
	int i;
	string s, tab;

	tab = "  ";
	if(ind === undefined) ind = "";
	s = ind + (string)this.t;
	if(this.type !== undefined)
		s += " [" + this.type.toDebug() + "]";
	if(this.isconst)
		s += " const";
	s += "\n";
	switch(this.t){
	case ASTMODULE:
		for(i = 0; i < this.stat.length; i++)
			s += this.stat[i].toDebug(ind + tab);
		break;
	case ASTUN: s += this.n1.toDebug(ind + tab); break;
	case ASTBIN:
	case ASTASSIGN:
		s += this.n1.toDebug(ind + tab);
		s += this.n2.toDebug(ind + tab);
		break;
	case ASTNUM:
		s += ind + tab + this.num.toString() + "\n";
		break;
	case ASTCINT:
		s += ind + tab + this.numi.toString() + "\n";
	}
	return s;
}

struct Symbol {
	string name;
	enum SymbolType {
		SYMPORT,
	} t;
	string dir;
	int lineno;
	bool portreg;
	Type type;
};

void
Symbol(string name)
{
	this.name = name;
}

string
Symbol.toString()
{
	return this.name;
}

struct Scope {
	Scope up;
	Symbol sym;
	Symbol tab[string];
};

function
Scope(Scope up, Symbol sym)
{
	this.up = up;
	this.sym = sym;
	this.tab = {};
}

struct Parser {
	string s;
	int idx;
	int lineno;
	Token peektok;
	Scope global;
	Scope scope;
	Token nextnum;
	string str;
};

function
Parser(s)
{
	this.str = s;
	this.idx = 0;
	this.lineno = 1;
	this.peektok = null;
	this.global = new Scope(null, null);
	this.scope = this.global;
	this.nextnum = null;
}

Symbol
Parser.getsym(string name)
{
	Scope sc;
	
	for(sc = this.scope; sc !== null; sc = sc.up)
		if(sc.tab[name] !== undefined && sc.tab[name].t !== undefined)
			return sc.tab[name];
	return this.scope.tab[name] = new Symbol(name);
}

void
Parser.newscope(Symbol sym)
{
	this.scope = new Scope(this.scope, sym);
}

void
Parser.scopeup()
{
	this.scope = this.scope.up;
}

function
error(int line, string str)
{
	throw "line " + line.toString() + ": " + str;
}

string
Parser.getc()
{
	string c;
	
	c = this.str.charAt(this.idx++);
	if(c == "\n") this.lineno++;
	return c;
}

void
Parser.ungetc()
{
	if(this.str.charAt(--this.idx) == "\n")
		this.lineno--;
}

bool
isspace(string s)
{
	return s == " " || s == "\t" || s == "\n";
}

bool
isdigit(string s)
{
	return s >= "0" && s <= "9";
}

bool
isident(string s)
{
	return s >= "A" && s <= "Z" || s >= "a" && s <= "z" || s >= "0" && s <= "9" || s == "_" || s == "$";
}

string
decconv(int lno, string s)
{
	string r, digs, p;
	int i;

	r = "0";
	digs = "00000 10000 01000 11000 00100 10100 01100 11100 00010 10010";
	for(i = 0; i < s.length; i++){
		p = s.charAt(i);
		if(p < "0" || p > "9")
			error(lno, "invalid character '" + p + "' in decimal number");
		r = mpmul(r, "01010");
		r = mpadd(r, digs.substr((s.charCodeAt(i) - 48) * 6, 5));
	}
	return r;
}

Const
numparse(int lno, Token a, Token b, Token c)
{
	string r, digs;
	int i, p;

	if(b == null)
		b = Token{base: 10, sign: true};
	switch(b.base){
	case 2:
		r = "";
		for(i = 0; i < c.str.length; i++){
			p = "01xXzZ?".indexOf(c.str[i]);
			if(p < 0)
				error(lno, "invalid character '" + c.str[i] + "' in binary number");
			r = "01xXzZ?"[p] + r;
		}
		if("01".indexOf(c.str[0]) >= 0)
			r = r + "0";
		break;
	case 8:
		digs = "000 100 010 110 001 101 110 111 xxx xxx zzz zzz zzz";
		r = "";
		for(i = 0; i < c.str.length; i++){
			p = "01234567xXzZ?".indexOf(c.str[i]);
			if(p < 0)
				error(lno, "invalid character '" + c.str[i] + "' in octal number");
			r = digs.substr(p * 4, 3) + r;
		}
		if("01234567".indexOf(c.str[0]) >= 0)
			r = r + "0";
		break;
	case 10:
		if(c.str.length == 1 && (i = "xXzZ?".indexOf(c.str)) >= 0)
			r = "xxzzz"[i];
		else
			r = decconv(lno, c.str);
		break;
	case 16:
		digs = "0000 1000 0100 1100 0010 1010 1100 1110 0001 1001 0101 1101 0011 1011 0111 1111 xxxx zzzz zzzz";
		r = "";
		for(i = 0; i < c.str.length; i++){
			p = "0123456789abcdefxz?".indexOf(c.str[i].toLowerCase());
			if(p < 0)
				error(lno, "invalid character '" + c.str[i] + "' in hexadecimal number");
			r = digs.substr(p * 5, 4) + r;
		}
		if("xz".indexOf(r[r.length - 1]) < 0)
			r = r + "0";
		break;
	
	default:
		error(lno, "unimplemented base " + b.base.toString());
	}
	if(a !== null){
		p = parseInt(a.str, 10);
		return new Const(b.sign ? mpxtend(r, p) : mptrunc(r, p), p, b.sign);
	}
	return new Const(mpnorm(r), null, b.sign);
}

string longop[string] = {
	"!=": "!=",
	"!==": "!==",
	"&&": "&&",
	"**": "**",
	"<<": "<<",
	"<<<": "<<<",
	"<=": "<=",
	"==": "==",
	"===": "===",
	">=": ">=",
	">>": ">>",
	">>>": ">>>",
	"^~": "~^",
	"||": "||",
	"~&": "~&",
	"~^": "~^",
	"~|": "~|"
};
bool opchar[string] = {
	"!": true,
	"&": true,
	"*": true,
	"<": true,
	"=": true,
	">": true,
	"^": true,
	"|": true,
	"~": true
};

Token
Parser.lex()
{
	string c, s, r;
	int i;
	Token t;
	
	if(this.peektok !== null){
		t = this.peektok;
		this.peektok = null;
		return t;
	}
	again: for(;;){
		do{
			c = this.getc();
		}while(isspace(c));
		if(c == "") return Token{t: "EOF"};
		if(c == "/"){
			c = this.getc();
			if(c == "/"){
				while(c = this.getc(), c != "" && c != "\n");
					;
				continue again;
			}else if(c == "*"){
				do{
					while(c = this.getc(), c != "" && c != "*")
						;
					while(c == "*")
						c = this.getc();
				}while(c != "" && c != "/");
				continue again;
			}else{
				this.ungetc();
				return Token{t: "/"};
			}
		}
		if(c == "\""){
			s = "";
			while(c = this.getc(), c != "\""){
				if(c == "\n")
					error(this.lineno, "nl in string");
				if(c == "")
					error(this.lineno, "eof in string");
				if(c == "\\")
					switch(c = this.getc()){
					case "n": s += "\n"; break;
					case "t": s += "\t"; break;
					default:
						if(isdigit(c)){
							r = "";
							i = 0;
							do
								r += c;
							while(++i < 3 && (c = this.getc(), isdigit(c)));
							if(i < 3){
								this.ungetc();
								s += "\\" + r;
							}else
								s += String.fromCharCode(parseInt(r, 8));
						}else
							s += c;
					}
				else
					s += c;
			}
			return Token{t: "str", str: s};
		}
		if(c == "\\"){
			s = "";
			while(c = this.getc(), c != "" && !isspace(c))
				s += c;
			return Token{t: "id", sym: this.getsym(s)};
		}
		if(isdigit(c) || this.nextnum && (isident(c) || c == "?")){
			s = c;
			if(s == "_") s = "";
			while(c = this.getc(), isident(c) || c == "?")
				if(c != "_")
					s += c;
			this.ungetc();
			this.nextnum = null;
			return Token{t: "num", str: s};
		}
		if(isident(c)){
			s = c;
			while(c = this.getc(), isident(c))
				s += c;
			this.ungetc();
			if(kwtab[s])
				return Token{t: "kw:" + s};
			return Token{t: "id", sym: this.getsym(s)};
		}
		if(c == "'"){
			s = c;
			c = this.getc();
			if(c == "s" || c == "S"){
				s += c;
				c = this.getc();
			}
			if("bodhBODH".indexOf(c) >= 0)
				s += c;
			else
				this.ungetc();
			return this.nextnum = Token{t: "base", base: {"b": 2, "B": 2, "o": 8, "O": 8, "d": 10, "D": 10, "h": 16, "H": 16}[c], sign: s[1] == "s" || s[1] == "S"};
		}
		if(opchar[c]){
			s = c;
			while(c = this.getc(), c != "" && longop[s + c] !== undefined)
				s += c;
			this.ungetc();
			return Token{t: longop[s] || s};
		}
		return Token{t: c};
	}
}

void
Parser.superman(Token t)
{
	if(this.peektok !== null)
		throw "This is not Superman IV";
	this.peektok = t;
}

Token
Parser.peek()
{
	if(this.peektok !== null)
		return this.peektok;
	return this.peektok = this.lex();
}

Token
Parser.expect(string tt)
{
	Token t;
	
	t = this.lex();
	if(t.t !== tt)
		error(this.lineno, "expected " + tt + ", got " + t.t);
	return t;
}

Token
Parser.expectany(bool tt[string])
{
	Token t;
	string s, i;
	bool first;
	
	t = this.lex();
	if(tt[t.t] !== true){
		s = "expected ";
		first = true;
		for(i in tt){
			if(!first) s += " or ";
			else first = false;
			s += i;
		}
		error(this.lineno, "expected " + s + ", got " + t.t);
	}
	return t;
}

Token
Parser.got(string tt)
{
	if(this.peek().t === tt)
		return this.lex();
	return null;
}

Token
Parser.gotany(bool tt[string])
{
	if(tt[this.peek().t] !== null)
		return this.lex();
	return null;
}

void
Parser.p_attributes()
{
}

Node
Parser.p_range[]()
{
	Node a, b;

	this.expect("[");
	a = this.p_expr();
	this.expect(":");
	b = this.p_expr();
	this.expect("]");
	return [a, b];
}

void
Parser.p_port_declaration()
{
	string dir;
	bool wire;
	bool signed;
	Node range[];
	Symbol sym;

	this.p_attributes();
	dir = this.expectany({"kw:input": true, "kw:output": true, "kw:inout": true}).t.substr(3);
	wire = this.expectany({"kw:wire": true, "kw:reg": true}).t == "kw:wire";
	signed = this.got("kw:signed") !== null;
	if(this.peek().t === "[")
		range = this.p_range();
	else
		range = null;
	sym = this.expect("id").sym;
	
	sym.t = SYMPORT;
	sym.dir = dir;
	sym.portreg = !wire;
	if(range === null)
		sym.type = signed ? sbittype : bittype;
	else
		sym.type = new Type(TYPBITV, signed, range[1], range[0], null);
}

void
Parser.p_delay3()
{
	if(this.got("#") !== null){
		this.expect("num");
	}
}

Node
Parser.p_hier_identifier()
{
	Symbol s;
	Node n;
	
	s = this.expect("id").sym;
	if(s.t === undefined)
		error(this.lineno, "undeclared '" + s.toString() + "'");
	n = new Node(Node{t: ASTSYM, sym: s, lineno: this.lineno});
/*	while(this.got(".") !== null)
		n = new Node(Node{t: "hier", n1: n, n2: this.expect("id").s});*/
	
	return n;
}

Node
Parser.p_lvalue()
{
	Node l[], n, a;
	
	if(this.got("{") !== null){
		l = [this.p_lvalue()];
		while(this.got(",") !== null)
			l.push(this.p_lvalue());
		this.expect("}");
		return new Node(Node{t: ASTCONCAT, l: l, lineno: this.lineno});
	}else{
		n = this.p_hier_identifier();
		while(this.got("[") !== null){
			a = this.p_expr();
			if(this.got(":") !== null)
				n = new Node(Node{t: ASTIDX, n1: n, n2: a, n3: this.p_expr(), lineno: this.lineno});
			else
				n = new Node(Node{t: ASTIDX, n1: n, n2: a, n3: null, lineno: this.lineno});
			this.expect("]");
		}
		return n;
	}
}

bool untab[string] = {
	"+": true,
	"-": true,
	"!": true,
	"~": true,
	"&": true,
	"~&": true,
	"|": true,
	"~|": true,
	"^": true,
	"~^": true
};

Node
Parser.p_primary()
{
	Token t, a, b, c;
	Node n;
	Const cn;

	switch(t = this.lex(), t.t){
	case "id":
		this.superman(t);
		return this.p_hier_identifier();
	case "num":
		b = this.got("base");
		if(b !== null){
			a = t;
			c = this.expect("num");
		}else{
			a = null;
			c = t;
		}
		cn = numparse(this.lineno, null, b, c);
		if(cn.sign && cn.v.length < 32 && cn.v.indexOf("x") < 0 && cn.v.indexOf("z") < 0)
			return new Node(Node{t: ASTCINT, numi: mptoi(cn.v), lineno: this.lineno});
		return new Node(Node{t: ASTNUM, num: cn, lineno: this.lineno});
	case "base":
		b = t;
		c = this.expect("num");
		cn = numparse(this.lineno, null, b, c);
		if(cn.sign && cn.v.length < 32 && cn.v.indexOf("x") < 0 && cn.v.indexOf("z") < 0)
			return new Node(Node{t: ASTCINT, numi: mptoi(cn.v), lineno: this.lineno});
		return new Node(Node{t: ASTNUM, num: cn, lineno: this.lineno});
	case "(":
		n = this.p_expr();
		this.expect(")");
		return n;
	default:
		if(untab[t.t] !== undefined){
			this.p_attributes();
			return new Node(Node{t: ASTUN, op: t.t, n1: this.p_primary(), lineno: this.lineno});
		}
		error(this.lineno, "unexpected " + t.t);
	}
}

var optab[string] = {
	"**": {prec: 11},
	"*": {prec: 10},
	"/": {prec: 10},
	"%": {prec: 10},
	"+": {prec: 9},
	"-": {prec: 9},
	"<<": {prec: 8},
	">>": {prec: 8},
	"<<<": {prec: 8},
	">>>": {prec: 8},
	"<": {prec: 7},
	"<=": {prec: 7},
	">": {prec: 7},
	">=": {prec: 7},
	"==": {prec: 6},
	"!=": {prec: 6},
	"===": {prec: 6},
	"!==": {prec: 6},
	"&": {prec: 5},
	"^": {prec: 4},
	"~^": {prec: 4},
	"|": {prec: 3},
	"&&": {prec: 2},
	"||": {prec: 1},
	"max": {prec: 0},
};

Node
Parser.p_expr_core()
{
	Node ex[];
	var op[];
	string t;
	Node a;
	
	ex = [this.p_primary()];
	op = [];
	while(optab[t = this.peek().t] !== undefined){
		while(op.length > 0 && optab[op[0]].prec >= optab[t].prec){
			a = ex.shift();
			ex[0] = new Node(Node{t: ASTBIN, op: op.shift(), n1: ex[0], n2: a, lineno: this.lineno});
		}
		this.lex();
		op.unshift(t);
		this.p_attributes();
		ex.unshift(this.p_primary());
	}
	while(op.length > 0){
		a = ex.shift();
		ex[0] = new Node(Node{t: ASTBIN, op: op.shift(), n1: ex[0], n2: a, lineno: this.lineno});
	}
	return ex[0];
}

Node
Parser.p_expr()
{
	Node p, q, l[], r;
	
	if(this.got("{") !== null){
		p = this.p_expr();
		if(this.got("{") !== null){
			q = this.p_expr();
			this.expect("}");
			this.expect("}");
			return new Node(Node{t: ASTREPL, n1: p, n2: q, lineno: this.lineno});
		}else{
			l = [p];
			while(this.got(",") !== null)
				l.push(this.p_expr());
			while(this.got(",") !== null);
			this.expect("}");
			return new Node(Node{t: ASTCONCAT, l: l, lineno: this.lineno});
		}
	}else{
		p = this.p_expr_core();
		if(this.got("?") !== null){
			q = this.p_expr();
			this.expect(":");
			r = this.p_expr();
			return new Node(Node{t: ASTTERN, n1: p, n2: q, n3: r, lineno: this.lineno});
		}else
			return p;
	}
	
}

Node
Parser.p_assignment()
{
	Node l, r;
	
	l = this.p_lvalue();
	this.expect("=");
	r = this.p_expr();
	return new Node(Node{t: ASTASSIGN, n1: l, n2: r, lineno: this.lineno});
}

Node
Parser.p_module_item[]()
{
	Token t;
	Node r[];
	
	r = [];
	switch(t = this.lex(), t.t){
	case "kw:assign":
		this.p_delay3();
		r.push(this.p_assignment());
		while(this.got(",") !== null)
			r.push(this.p_assignment());
		this.expect(";");
		break;
	default:
		error(this.lineno, "unexpected " + t.t);
	}
	return r;
}

Node
Parser.p_module()
{
	Symbol s;
	Node n;
	int lno;
	Node r[];

	this.p_attributes();
	this.expect("kw:module");
	lno = this.lineno;
	s = this.expect("id").sym;
	this.newscope(s);
	try{
		if(this.got("(") !== null && this.got(")") === null){
			this.p_port_declaration();
			while(this.got(",") !== null)
				this.p_port_declaration();
			this.expect(")");
		}
		this.expect(";");
		r = [];
		while(this.got("kw:endmodule") === null)
			r.push.apply(r, this.p_module_item());
		n = new Node(Node{t: ASTMODULE, sym: s, stat: r, lineno: lno});
	}finally{
		this.scopeup();
	}
	return n;
}

void
parse()
{
	string s;
	Parser p;
	Node m;
	var e;
	
	s = document.getElementById("code").value;
	localStorage.setItem("text", s);
	p = new Parser(s);
	try{
		m = p.p_module();
		typecheck(m, null);
		document.getElementById("out").innerHTML = m.toDebug();
	}catch(e){
		document.getElementById("out").innerHTML = e;
	}
}
