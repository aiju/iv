int
max(int a, int b)
{
	return a > b ? a : b;
}

string
mpdig(string s, int i)
{
	string t;
	
	t = s.charAt(i);
	if(t == "") return s.charAt(s.length - 1);
	return t;
}

string
mpnorm(string r)
{
	int i;

	i = r.length - 2;
	while(i >= 0 && r.charAt(i) == r.charAt(r.length - 1))
		i--;
	return r.substr(0, i + 2);
}

string
mpadd(string a, string b)
{
	int i, m, c;
	string r;
	
	m = max(a.length, b.length) + 1;
	c = 0;
	r = "";
	for(i = 0; i < m; i++){
		c += mpdig(a, i) == "1" ? 1 : 0;
		c += mpdig(b, i) == "1" ? 1 : 0;
		r += String.fromCharCode(48 + (c & 1));
		c >>= 1;
	}
	return mpnorm(r);
}

string
mpsub(string a, string b)
{
	int i, m, c;
	string r;
	
	m = max(a.length, b.length) + 1;
	c = 1;
	r = "";
	for(i = 0; i < m; i++){
		c += mpdig(a, i) == "1" ? 1 : 0;
		c += mpdig(b, i) == "1" ? 0 : 1;
		r += String.fromCharCode(48 + (c & 1));
		c >>= 1;
	}
	return mpnorm(r);
}

string
mpmul(string a, string b)
{
	int i;
	string r;
	
	r = "0";
	for(i = 0; i < b.length - 1; i++)
		if(mpdig(b, i) == "1")
			r = mpadd(r, "0".repeat(i) + a);
	if(mpdig(b, i) == "1")
		r = mpsub(r, "0".repeat(i) + a);
	return r;
}

string
mptrunc(string a, int n)
{
	return mpnorm(a.substr(0, n) + "0");
}

string
mpxtend(string a, int n)
{
	return mpnorm(a.substr(0, n));
}

int
mptoi(string n)
{
	int i, r;
	
	r = -parseInt(n.charAt(n.length - 1));
	for(i = n.length - 2; i >= 0; i--)
		r = r * 2 + parseInt(n.charAt(i));
	return r;
}
